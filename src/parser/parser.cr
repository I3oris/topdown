require "./syntax_error"
require "./tokens"

# `TopDown::Parser` is the main class to derive for building a parser.
#
# ### Basis
#
# A minimal parser could be:
# ```
# class Parser < TopDown::Parser
#   root :expression
#
#   syntax :expression do
#   end
# end
# ```
# which will parse anything but EOF.
#
# `root` indicate the starting point of parsing, the rest is to do inside `syntax`.
#
# Syntax are like functions, in which, any code can be added.
# Inside it, `parse` & `parse!` can be used to define what to parse.
#
# ```
# syntax :expression do
#   a = parse('a')
#   parse(/( )+/)
#   foo = parse!("foo")
#
#   {a, foo}
# end
# ```
#
# ```
# "a   foo" # ~> {'a', "foo"}
# "a   bar" # ~> Unexpected character 'b', expected 'foo'
# "ab"      # ~> Unexpected character 'b', expected expression
# ```
# NOTE:
# In this documentation, `"foo" # ~> result` is used as a shortcut for
# `Parser.new("foo").parse # => result`
#
#
# In the above, if `parse!("foo")` fail to parse, it raises an error.
# But `parse('a')` don't raises, instead it use a `break` (`break Fail.new`) to stop the current sequence to be parsed.
# This failure is caught by the `root :expression`, that why it next raises an exception.
#
# This difference is important because is on what is based `TopDown`, using `parse` let a change to the surrounding context to handle the failure, whereas
# `parse!` permit the raise directly, leading to better errors.
#
# For example, using `parse!` above:
# ```
# syntax :expression do
#   a = parse!('a')
#   parse!(/( )+/)
#   foo = parse!("foo")
#
#   {a, foo}
# end
# ```
#
# ```
# "ab" # ~> Unexpected character 'b', expected pattern /( )+/
# ```
# Gives a more precise error.
#
# ### Repeat, union, maybe
#
# `repeat`, `union`, `maybe` allow interesting things to be parsed. They take both a block, in which parse failure can occur.
#
# ```
# syntax :expression do
#   repeat do
#     parse('a')
#     parse!('b')
#   end
#   maybe { parse('c') }
#   parse!('d')
#
#   union do
#     parse('e')
#     parse('Ã©')
#     parse('Ã¨')
#   end
#   "ok"
# end
# ```
#
# This is equivalent to parse `/(ab)*c?d[eÃ©Ã¨]/`, in a bit more verbose. However, doing so allow to insert code between and retrieve
# only needed result, storing in array, or to insert some logic.
#
# For example:
# ```
# syntax :expression do
#   array = [] of {Char, Char}
#   repeat do
#     array << {parse('a'), parse!('b')}
#   end
#   have_c = maybe { parse('c') }
#   parse!('d')
#
#   if have_c
#     e = union do
#       parse('e')
#       parse('Ã©')
#       parse('Ã¨')
#     end
#   end
#   {array, e}
# end
# ```
# Here, we store each `ab` in an array and accept the union `'e'|'Ã©'|'Ã¨'` only if the optional `'c'` have been parsed.
# ```
# "ababd" # ~> {[{'a', 'b'}, {'a', 'b'}], nil}
# "cde"   # ~> {[], 'e'}
# "dÃ©"    # ~> Unexpected character 'Ã©', expected 'EOF'
# "abac"  # # Unexpected character 'c', expected 'b'
# ```
#
# NOTE:
# When using `repeat`, `union`, and `maybe`, we always use `parse` at first (without exclamation), because they `break` on failure,
# which is caught by the `repeat`/`maybe`/`union`. They know so they should continue parsing without a failure.
#
# ### Infix
#
# `infix` can be used inside a union. It permits to parse operators with precedence easily.
# ```
# syntax :expression do
#   union do
#     parse(/\d+/).to_i
#     infix(30, :pow)
#     infix(20, :mul)
#     infix(20, :div)
#     infix(10, :add)
#     infix(10, :sub)
#   end
# end
#
# syntax :pow, "**" { left() ** parse!(:expression) }
# syntax :mul, '*' { left() * parse!(:expression) }
# syntax :div, '/' { left() / parse!(:expression) }
# syntax :add, '+' { left() + parse!(:expression) }
# syntax :sub, '-' { left() - parse!(:expression) }
# ```
#
# Infix are treated specially by the `union`. They are parsed after any other member of the union, the `left()` result is updated each time.
# They are parsed in function of their precedence (first argument), higher precedence mean grouped first.
#
# ```
# "3*6+6*4" # ~> 42
# ```
#
# ### Precedence:
#
# `current_precedence()` is initially zero, it changes when entering in `infix`. This is this value that guide the recursion for parsing operators.
# Its value can be changed at specific places, so parsing order can be entirely controlled.
#
# For example, assuming we want to parse a ternary if `_ ? _ : _` that have a higher precedence that classical operators (e.g. `"1+1?2:3+3"` gets parsed as (`1` + (`1?2:3`)) + `3`.
#
# We can do the following:
# ```
# syntax(:expression) do
#   union do
#     parse(/\d+/).to_i
#     infix 10, :plus
#     infix 90, :ternary_if
#   end
# end
#
# syntax :plus, '+' do
#   left() + parse!(:expression)
# end
#
# syntax :ternary_if, '?' do
#   cond = left()
#   then_arm = parse!(:expression)
#   parse!(':')
#   else_arm = parse!(:expression)
#
#   cond != 0 ? then_arm : else_arm
# end
# ```
#
# ```
# "1+1?2:3+3" # ~> 6
# ```
#
# However the following is not what we except:
# ```
# "1+1?2+2:3+3" # => Unexpected character '+', expected ':'
# ```
#
# This happens because the `+` inside the `then_arm` have lower precedence, it wants so
# let its left with higher precedence finish before parsing itself. So the `then_arm` finish
# but we except a `:` right after, so it fails (got '+', expected ':').
#
# To fix that, we can set the `current_precedence()` for the `then arm` to 0:
# ```
# then_arm = parse!(:expression, with_precedence: 0)
# ```
# ```
# "1+1?2+2:3+3" # ~> 8
# ```
#
abstract class TopDown::Parser < TopDown::CharReader
  # Parses the source contained in this parser.
  #
  # Returns the result of the root syntax.
  # Expects `eof` after parsing root syntax.
  # Raises `SyntaxError` if fail to parse.
  def parse
    result = parse_root
    if result.is_a? Fail
      raise_syntax_error error_message(->hook_could_not_parse_syntax(Char, Symbol), got: peek_char, expected: :root)
    end

    consume_char!('\0')
    result
  end

  private def parse_root
    {% raise "Root syntax is not defined, use 'TopDown::Parser.root' macro to define the root" %}
  end

  # Defines the main syntax to parse.
  #
  # *parselet*: the syntax name or the parselet to be the root.
  # Could be `StringLiteral`|`CharLiteral`|`RegexLiteral`|`SymbolLiteral`|`ArrayLiteral`|`Call`,
  # see [`Parser.parse`](#parse(parselet,with_precedence=nil,&block)-macro)
  macro root(parselet)
    private def parse_root
      parse!({{parselet}}, with_precedence: 0)
    end
  end

  # Defines a syntax that can be called with `parse(SymbolLiteral)`.
  #
  # TODO: docs
  macro syntax(syntax_name, *prefixs, &block)
    @[AlwaysInline]
    private def parse_{{syntax_name.id}}(_left_, _precedence_)
      _begin_location_ = self.location

      handle_fail do
        prefixs = Tuple.new({% for p in prefixs %} parse({{p}}), {% end %})

        {% if block %}
          handle_fail(*prefixs) {{block}}
        {% end %}
      end
    end
  end

  private macro consume_char(char)
    skip_chars
    if peek_char != {{char}}
      break Fail.new
    else
      next_char
    end
  end

  private macro consume_char!(char, error = nil, at = nil)
    skip_chars
    if peek_char != {{char}}
      raise_syntax_error error_message({{error}} || ->hook_unexpected_char(Char, Char), got: peek_char, expected: {{char}}), at: ({{at || "self.location".id}})
    else
      next_char
    end
  end

  private macro consume_not_char(char)
    skip_chars
    if peek_char == {{char}} || peek_char == '\0'
      break Fail.new
    else
      next_char
    end
  end

  private macro consume_any_char
    if peek_char == '\0'
      break Fail.new
    else
      next_char
    end
  end

  private macro consume_range(range)
    if peek_char.in? {{range}}
      next_char
    else
      break Fail.new
    end
  end

  private macro consume_range!(range, error = nil, at = nil)
    if peek_char.in? {{range}}
      next_char
    else
      raise_syntax_error error_message({{error}} || ->hook_unexpected_range_char(Char, Range(Char, Char)), got: peek_char, expected: {{range}}), at: ({{at || "self.location".id}})
    end
  end

  private macro consume_string(string)
    skip_chars
    capture do
      no_skip do
        {% for c in string.chars %}
          consume_char({{c}})
        {% end %}
      end
    end
  end

  private macro consume_string!(string, error = nil, at = nil)
    skip_chars
    %result = handle_fail do
      capture do
        no_skip do
          {% for c in string.chars %}
            consume_char({{c}})
          {% end %}
        end
      end
    end
    if %result.is_a? Fail
      raise_syntax_error error_message({{error}} || ->hook_could_not_parse_string(Char, String), got: peek_char, expected: {{string}}), at: ({{at || "self.location".id}})
    end
    %result
  end

  private macro consume_not_string(string)
    %old_location = self.location
    %result = handle_fail do
      consume_string({{string}})
    end
    if %result.is_a? Fail
      self.location = %old_location
      break Fail.new if peek_char == '\0'
      next_char
      nil
    else
      break Fail.new
    end
  end

  private macro consume_regex(regex)
    skip_chars
    if regex_match_start({{regex}}) =~ String.new(self.source.to_slice[self.location.pos..])
      @char_reader.pos += $0.bytesize
      $0.each_char { |ch| increment_location(ch) }
      $0
    else
      break Fail.new
    end
  end

  private macro consume_regex!(regex, error = nil, at = nil)
    skip_chars
    if regex_match_start({{regex}}) =~ String.new(self.source.to_slice[self.location.pos..])
      @char_reader.pos += $0.bytesize
      $0.each_char { |ch| increment_location(ch) }
      $0
    else
      raise_syntax_error error_message({{error}} || ->hook_could_not_parse_regex(Char, Regex), got: peek_char, expected: {{regex}}), at: ({{at || "self.location".id}})
    end
  end

  private macro consume_syntax(syntax_name, with_precedence = nil)
    skip_chars
    %result = parse_{{syntax_name.id}}(nil, {{with_precedence || "_precedence_".id}})
    if %result.is_a? Fail
      break Fail.new
    else
      %result
    end
  end

  private macro consume_syntax!(syntax_name, error = nil, at = nil, with_precedence = nil)
    skip_chars
    %result = parse_{{syntax_name.id}}(nil, {{with_precedence || "_precedence_".id}})
    if %result.is_a? Fail
      raise_syntax_error error_message({{error}} || ->hook_could_not_parse_syntax(Char, Symbol), got: peek_char, expected: {{syntax_name}}), at: ({{at || "self.location".id}})
    else
      %result
    end
  end

  private macro consume_not(parselet)
    {% if parselet.is_a? CharLiteral %}
      consume_not_char({{parselet}})
    {% elsif parselet.is_a? StringLiteral %}
      consume_not_string({{parselet}})
    {% elsif parselet.is_a? ArrayLiteral && parselet.size == 1 %}
      consume_not_token({{parselet[0]}})
    {% else %}
      {% raise "'not' arguments should be 'CharLiteral', 'StringLiteral' or 'ArrayLiteral' not #{parselet.class_name}: #{parselet}" %}
    {% end %}
  end

  # Parse the given *parselet*.
  #
  # #### parselet:
  # *parselet* could be one of the following:
  # * a `CharLiteral`, to parse exactly one `Char`
  # * a `StringLiteral`, to parse an exact `String`
  # * a `RegexLiteral`, to parse the given pattern, returns the matched `String`. ($~, $0, $1, ... could be used after)
  # * a `RangeLiteral(Char, Char)`, to parse any `Char` between the range
  # * a `SymbolLiteral`, to parse an entire `syntax`, returns the result of the syntax.
  # * an one-value `ArrayLiteral`, to parse a `Token`, returns the value of matched token.
  #  NOTE: the type of token should correspond to the type of tokens defined with `Parser.tokens`.
  # * a `Call`:
  #   * `|`: to parse a union, see `Parser.union`.
  #   * `any`, to parse any `Char` except EOF.
  #   * `[any]`, to parse any token except EOF.
  #   * `not(parselet)`, to parse any char, except if *parselet* matches.
  #  ```
  # parse('ðŸ’Ž')   # => 'ðŸ’Ž'
  # parse("foo") # => "foo"
  # parse(/\d+/, &.to_i)
  # parse(/"(([^"\\]|\\.)*)"/) { $1 }
  # parse(:expression)
  # parse([:"+="])           # => "+="
  # parse([TokenType::PLUS]) # => "+"
  #
  # parse("foo" | :value | 'ðŸ’Ž')
  # # equivalent to:
  # union do
  #   parse("foo")
  #   parse(:value)
  #   parse('ðŸ’Ž')
  # end
  #
  # parse(any)         # any char except '\0'
  # parse([any])       # any token except EOF
  # parse(not('\n'))   # any char except '\n' & EOF
  # parse(not([:"+"])) # any token except :"+" & EOF
  # parse(not("foo"))  # any char or fail on "foo".
  #  ```
  #
  # #### failure:
  # If the given *parselet* fails to parse, it `break` the current sequence with a `Fail`. Failure is catch by the surrounding context.
  # * inside an `union`, tell the union that member have fail. The union tries to parse the next member.
  # * inside a `maybe`, the maybe will return `nil`.
  # * inside a `repeat`, make the repeat to stop.
  # * inside a `syntax`, the syntax is considered to fail, it will in turn `break` or raises.
  #
  # #### block:
  # A *block* could be given to let return the value of the block.
  #
  # #### precedence:
  # *with_precedence* (`NumberLiteral`) changes the `current_precedence`, the given *parselet* will be parsed as if the contained syntax have this precedence.
  # Allow to handle multi-precedence for ternary-or-more operator.
  #
  macro parse(parselet, with_precedence = nil, &block)
    {% parselet = parselet.expressions[0] if parselet.is_a?(Expressions) && parselet.expressions.size == 1 %}

    {% if parselet.is_a? CharLiteral %}
      %result = consume_char({{parselet}})

    {% elsif parselet.is_a? RangeLiteral %}
      %result = consume_range({{parselet}})

    {% elsif parselet.is_a? StringLiteral %}
      %result = consume_string({{parselet}})

    {% elsif parselet.is_a? RegexLiteral %}
      %result = consume_regex({{parselet}})

    {% elsif parselet.is_a? SymbolLiteral %}
      %result = consume_syntax({{parselet}}, with_precedence: {{with_precedence}})

    {% elsif parselet.is_a? Call && parselet.name == "|" %}
      %result = simple_union([parse({{parselet.receiver}}), parse({{parselet.args[0]}})], with_precedence: {{with_precedence || "_precedence_".id}}) # || 0 ?

    {% elsif parselet.is_a? Call && parselet.name == "not" %}
      %result = consume_not({{parselet.args[0]}})

    {% elsif parselet.is_a? Call && parselet.name == "any" %}
      %result = consume_any_char

    {% elsif parselet.is_a? ArrayLiteral && parselet.size == 1 && parselet[0].is_a? Call && parselet[0].name == "any" %}
      %result = consume_any_token

    {% elsif parselet.is_a? ArrayLiteral && parselet.size == 1 %}
      %result = consume_token({{parselet[0]}})

    {% else %}
      {% raise "Unsupported ASTNode #{parselet.class_name} : #{parselet}" %}
    {% end %}

    {% if block %}
      handle_fail(%result) {{ block }}
    {% end %}
  end

  # Similar to [`Parser.parse`](#parse(parselet,with_precedence=nil,&block)-macro), but raises `SyntaxError` if the parsing fail.
  #
  # *error*: allows to customize error message, it can be:
  # * a `StringLiteral`:
  #   Following percent interpolations are available:
  #   * `%{got}`: The character or token causing the failure.
  #   * `%{expected}`: The expected `Char`, `String`, `Regex`, `Token` or syntax name (`Symbol`).
  # * a `ProcLiteral` taking two arguments: 'got' and 'expected', with types explained above.
  #
  # *at*: indicates where the error is raised, it can be a `Location`, `Range(Location, Location)` or `Range(Location, Nil)`.
  #
  # #### failure:
  # Because it raises, this shouldn't be used as first *parselet* inside a `union`, `maybe`, `repeat` and `syntax`.
  # This would raises before the surrounding context could try an other solution.
  #
  # However, this should generally be used everywhere else, to allow more localized errors.
  macro parse!(parselet, error = nil, at = nil, with_precedence = nil, &block)
    {% if parselet.is_a? CharLiteral %}
      %result = consume_char!({{parselet}}, error: {{error}}, at: {{at}})

    {% elsif parselet.is_a? RangeLiteral %}
      %result = consume_range!({{parselet}}, error: {{error}}, at: {{at}})

    {% elsif parselet.is_a? StringLiteral %}
      %result = consume_string!({{parselet}}, error: {{error}}, at: {{at}})

    {% elsif parselet.is_a? RegexLiteral %}
      %result = consume_regex!({{parselet}}, error: {{error}}, at: {{at}})

    {% elsif parselet.is_a? SymbolLiteral %}
      %result = consume_syntax!({{parselet}}, error: {{error}}, at: {{at}}, with_precedence: {{with_precedence}})

    {% elsif parselet.is_a? ArrayLiteral && parselet.size == 1 %}
      %result = consume_token!({{parselet[0]}}, error: {{error}}, at: {{at}})

    {% elsif parselet.is_a? Call && parselet.name == "|" %}
      %result = simple_union([parse({{parselet.receiver}}), parse({{parselet.args[0]}})], with_precedence: {{with_precedence || "_precedence_".id}})

    {% else %}
      {% raise "Unsupported ASTNode #{parselet.class_name} : #{parselet}" %}
    {% end %}

    {% if block %}
      handle_fail(%result) {{ block }}
    {% end %}
  end

  # TODO: docs
  macro infix(precedence, parselet, associativity = "right")
    {% if parselet.is_a? SymbolLiteral %}
      if _precedence_ < {{precedence}}
        {% precedence -= 1 if associativity.id == "right".id %}
        skip_chars
        _left_ = forward_fail(parse_{{parselet.id}}(_left_, _precedence_: {{precedence}}))
      else
        break Fail.new
      end
    {% else %}
      {% raise "parselet for infix should be 'SymbolLiteral', not '#{parselet.class_name.id}'" %}
    {% end %}
  end

  # In the context of an `infix` member, returns the `left` result of the current `union`.
  #
  # Returns `nil` in a context other than inside an `infix`.
  #
  # Each time a `union` member is successfully parsed, `left` value is updated.
  #
  # As a `union` continue parsing `infix` until possible,
  # `left` allows to retrieve the subsequent results.
  #
  # ```
  # syntax(:exp) do
  #   union do
  #     parse('a')
  #     infix(1, :comma)
  #   end
  # end
  # ```
  # ```
  # syntax(:comma) do
  #   l = left() # left returns either
  #   r = parse(:exp)
  #   "#{l}, #{r}"
  # end
  # ```
  macro left
    _left_
  end

  # Returns the current precedence.
  #
  # Initially zero.
  # Changes inside an `infix` or when `with_precedence` is used.
  macro current_precedence
    _precedence_
  end

  # Returns the `Location` at the beginning of the `syntax`.
  #
  # The given location is after skipping characters.
  macro begin_location
    _begin_location_
  end

  private macro simple_union(members, with_precedence)
    forward_fail(
      handle_fail(self.location, {{with_precedence}}) do |old_location, _precedence_|

        {% for union_member in members %}
          self.location = old_location
          %result = handle_fail do
            {{ union_member }}
          end

          break %result if !%result.is_a? Fail
        {% end %}

        self.location = old_location
        Fail.new
      end
    )
  end

  private macro _union_(&members)
    {% members = members.body.is_a?(Expressions) ? members.body.expressions : [members.body] %}
    {% prefix_members = members.reject do |m|
         m.is_a?(Call) && m.name == "infix"
       end %}

    {% infixs_members = members.select do |m|
         m.is_a?(Call) && m.name == "infix"
       end %}

    _left_ = simple_union({{prefix_members}}, with_precedence: 0) # precedence = 0 because prefix are not subject to precedence

    {% unless infixs_members.empty? %}
      loop do
        simple_union({{infixs_members}}, with_precedence: _precedence_)
      end
    {% end %}
    _left_
  end

  # Tries to parse each member of the union, returns the result of the first that succeed.
  #
  # ```
  # union do
  #   parse('1')
  #   parse('2')
  #   sequence do
  #     parse('a')
  #     parse('b')
  #     parse!('c')
  #   end
  #   parse('a')
  # end
  # ```
  # ```
  # "1"   # ~> '1'
  # "abc" # ~> 'c'
  # "ab*" # ~> "Unexpected character '*', expected 'c'"
  # "a"   # ~> 'a'
  # "*"   # ~> "Could not parse syntax ':main'
  # ```
  #
  # #### members:
  # Members are delimited by each expression in the `Expressions` of the given block.
  # NOTE: a block `begin`/`end` doesn't group a member since it is inlined by the crystal parser. Use `sequence` instead.
  #
  # #### failure:
  # If all members of the union fail, the union is considered to fail, and will `break` the current sequence
  #
  # #### infix:
  # `infix` members could be added to a union.
  #
  # `infix` members are always treated after each normal members, in the order there are defined.
  # A union act as follow:
  # 1) Tries to parse each normal member.
  # 2) When one succeed, store the result. `left` allows to retrieve this result.
  # 3) Tries to parse `infix` members whose precedence is greater than current precedence (initial is zero).
  # 4) Inner of `infix` is executed, it potentially triggers parsing of this union recursively, but current precedence is sets to the `infix` precedence.
  # 5) When one fully succeed, store the result. `left` is updated.
  # 6) Repeats step 3-5) until there no more infix.
  # 7) Returns last result.
  #
  # This is mainly the top-down operator precedence algorithm, also known as precedence climbing.
  macro union(&members)
    _union_ {{members}}
  end

  # Parses the sequence inside the block, returns `nil` if it fails.
  #
  # ```
  # x = parse('1').to_i
  # y =
  #   maybe do
  #     parse('+')
  #     parse!('1').to_i
  #   end
  # parse!(';')
  # x + (y || 0)
  # ```
  # ```
  # "1;"   # ~> 1
  # "1+1;" # ~> 2
  # "1+*;" # ~> "Unexpected character '*', expected '1'"
  # "1*;"  # ~> "Unexpected character '*', expected ';'"
  # ```
  macro maybe(&)
    %old_location = self.location
    %result = handle_fail do
      {{ yield }}
    end
    if %result.is_a? Fail
      self.location = %old_location
      nil
    else
      %result
    end
  end

  # Repeatedly parses the sequence inside the block until it fails.
  #
  # Returns `nil`. Results should be either collected by `capture` or stored inside a variable or array.
  # ```
  # x = parse('1').to_i
  # repeat do
  #   parse('+')
  #   x += parse!('1').to_i
  # end
  # parse!(';')
  # x
  # ```
  # ```
  # "1;"         # ~> 1
  # "1+1+1+1+1;" # ~> 5
  # "1+1+1+*;"   # ~> "Unexpected character '*', expected '1'"
  # "1*;"        # ~> "Unexpected character '*', expected ';'"
  # ```
  macro repeat(&)
    %old_location = self.location
    loop do
      %old_location = self.location
      {{ yield }}
    end
    self.location = %old_location
    nil
  end

  # Repeatedly parses the sequence inside the block until it fails, with a *separator* parselet between each iteration.
  #
  # Returns `nil`. Results should be either collected by `capture` or stored inside a variable or array.
  # ```
  # x = 0
  # parse('(')
  # repeat(',') do
  #   x += parse('1').to_i
  # end
  # parse!(')')
  # x
  # ```
  # ```
  # "()"          # ~> 0
  # "(1,1,1,1,1)" # ~> 5
  # "(1,1,)"      # ~> "Unexpected character ',', expected ')'"
  # "(11)"        # ~> "Unexpected character '1', expected ')'"
  # ```
  macro repeat(separator, &)
    maybe do
      {{ yield }}
      repeat do
        parse({{separator}})
        {{ yield }}
      end
    end
  end

  # Equivalent to `repeat { union { ... } }`
  macro repeat_union(&block)
    repeat do
      _union_ do
        {{block.body}}
      end
    end
  end

  # Equivalent to `maybe { union { ... } }`
  macro maybe_union(&block)
    maybe do
      _union_ do
        {{block.body}}
      end
    end
  end

  private def skip_chars
  end

  # TODO: docs
  macro skip(&members)
    private def skip_chars
      if @no_skip_nest == 0
        no_skip do
          loop do
            _union_ do
              {{ yield }}
            end
          end
        end
      end
    end
  end

  @no_skip_nest = 0

  # TODO: docs
  macro no_skip(&)
    begin
      @no_skip_nest += 1
      %ret = ({{ yield }})
    ensure
      @no_skip_nest -= 1
    end
    %ret
  end

  # Empty struct representing a parse failure.
  #
  # When [`Parser.parse`](#parse(parselet,with_precedence=nil,&block)-macro) is used,
  # if it fails, it `break` the current sequence with a `Fail`.
  #
  # However when the method is surrounded by a block, the `Fail` get returned.
  # In this case it recommended to use `Parser.forward_fail` to `break` again.
  #
  # ```
  # result = Array.new(3) do
  #   parse("a")
  # end
  #
  # typeof(result) # => Array(String) | TopDown::Parser::Fail
  #
  # result = forward_fail(result)
  # typeof(result) # => Array(String)
  # ```
  struct Fail
  end

  private def handle_fail(*args)
    yield *args
  end

  # Returns the given *result*, or `break` the current sequence if *result* is a `Fail`.
  #
  # This macro is the recommended ways to handle `Fail`.
  #
  # ```
  # result = Array.new(3) do
  #   parse("a")
  # end
  #
  # typeof(result) # => Array(String) | TopDown::Parser::Fail
  #
  # result = forward_fail(result)
  # typeof(result) # => Array(String)
  # ```
  macro forward_fail(result)
    %result = {{result}}
    if %result.is_a? Fail
      break Fail.new
    else
      %result
    end
  end

  # Captures all characters parsed inside the *block*.
  #
  # Returns a `String`
  # NOTE: `skip` characters are still captured, use `partial_capture` instead.
  #
  # ```
  # capture do
  #   repeat(/ +/) do
  #     parse(/\w+/)
  #   end
  # end
  # ```
  # ```
  # "a   bc d e"  # ~> "a   bc d e"
  # "Hello World" # ~> "Hello World"
  # ```
  macro capture(&)
    %pos = self.location.pos
    {{ yield }}
    String.new(self.source.to_slice[%pos...self.location.pos])
  end

  # Captures chosen characters parsed inside the *block*.
  #
  # Yields a `String::Builder` in which characters to capture can be added.
  #
  # Returns a `String`.
  #
  # ```
  # partial_capture do |io|
  #   repeat(/ +/) do
  #     io << parse(/\w+/)
  #   end
  # end
  # ```
  # ```
  # "a   bc d e"  # ~> "abcde"
  # "Hello World" # ~> "HelloWorld"
  # ```
  macro partial_capture(&block)
    {% raise "partial_capture should have one block argument" if !block || block.args.size != 1 %}
    {{block.args[0].id}} = String::Builder.new
    {{ yield }}
    {{block.args[0].id}}.to_s
  end

  # TODO: docs
  macro sequence(&)
    {{ yield }}
  end

  # Appends a '\A' to begin of *regex* (forcing the regex to match at start)
  # Equivalent to `/\A#{regex}/` but done at compile time
  private macro regex_match_start(regex)
    {%
      str = "(?"
      str += 'i' if regex.options.includes?(:i)
      str += "ms" if regex.options.includes?(:m)
      str += 'x' if regex.options.includes?(:x)
      str += '-'
      str += 'i' unless regex.options.includes?(:i)
      str += "ms" unless regex.options.includes?(:m)
      str += 'x' unless regex.options.includes?(:x)
      str += ':'
      str += regex.source
      str += ')'
    %}
    %r(\A{{str.id}})
  end
end
